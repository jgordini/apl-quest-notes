⍝ Write a function that:
  ⍝ takes a non-empty non-scalar array right argument
  ⍝ returns an array of the same shape as the argument, but with pairs of elements along the last axis “flipped”. If the array has an odd number of elements in the last axis, leave the last element unchanged.

n
⍝ ┌────────┬──────┬────────┬────────┬────────┐
⍝ │Griselda│Upton │Prospero│Delicia │Topaza  │
⍝ ├────────┼──────┼────────┼────────┼────────┤
⍝ │Clyde   │Ellis │Gilles  │Hilary  │Sheridan│
⍝ └────────┴──────┴────────┴────────┴────────┘
⍝ ┌────────┬──────┬────────┬────────┬────────┐
⍝ │Carole  │Clay  │Harley  │Bellance│Nesta   │
⍝ ├────────┼──────┼────────┼────────┼────────┤
⍝ │Dorian  │Sigurd│Clinton │Renee   │Nada    │
⍝ └────────┴──────┴────────┴────────┴────────┘
n[1;1;]
⍝ ┌────────┬─────┬────────┬───────┬──────┐
⍝ │Griselda│Upton│Prospero│Delicia│Topaza│
⍝ └────────┴─────┴────────┴───────┴──────┘
{⍴⍵}n[1;1;]
⍝ 5
{1 0⍴⍨⍴⍵}n[1;1;]
⍝ 1 0 1 0 1
{⍵⊂⍨1 0⍴⍨⍴⍵}n[1;1;]
⍝ ┌────────────────┬──────────────────┬────────┐
⍝ │┌────────┬─────┐│┌────────┬───────┐│┌──────┐│
⍝ ││Griselda│Upton│││Prospero│Delicia│││Topaza││
⍝ │└────────┴─────┘│└────────┴───────┘│└──────┘│
⍝ └────────────────┴──────────────────┴────────┘
{⌽¨⍵⊂⍨1 0⍴⍨⍴⍵}n[1;1;]
⍝ ┌────────────────┬──────────────────┬────────┐
⍝ │┌─────┬────────┐│┌───────┬────────┐│┌──────┐│
⍝ ││Upton│Griselda│││Delicia│Prospero│││Topaza││
⍝ │└─────┴────────┘│└───────┴────────┘│└──────┘│
⍝ └────────────────┴──────────────────┴────────┘
{,/⌽¨⍵⊂⍨1 0⍴⍨⍴⍵}n[1;1;]
⍝ ┌────────────────────────────────────────┐
⍝ │┌─────┬────────┬───────┬────────┬──────┐│
⍝ ││Upton│Griselda│Delicia│Prospero│Topaza││
⍝ │└─────┴────────┴───────┴────────┴──────┘│
⍝ └────────────────────────────────────────┘
{⊃,/⌽¨⍵⊂⍨1 0⍴⍨⍴⍵}n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┬──────┐
⍝ │Upton│Griselda│Delicia│Prospero│Topaza│
⍝ └─────┴────────┴───────┴────────┴──────┘
E←{⊃,/⌽¨⍵⊂⍨1 0⍴⍨⍴⍵}⍤1
n
⍝ ┌────────┬──────┬────────┬────────┬────────┐
⍝ │Griselda│Upton │Prospero│Delicia │Topaza  │
⍝ ├────────┼──────┼────────┼────────┼────────┤
⍝ │Clyde   │Ellis │Gilles  │Hilary  │Sheridan│
⍝ └────────┴──────┴────────┴────────┴────────┘
⍝ ┌────────┬──────┬────────┬────────┬────────┐
⍝ │Carole  │Clay  │Harley  │Bellance│Nesta   │
⍝ ├────────┼──────┼────────┼────────┼────────┤
⍝ │Dorian  │Sigurd│Clinton │Renee   │Nada    │
⍝ └────────┴──────┴────────┴────────┴────────┘
E n
⍝ ┌──────┬────────┬────────┬────────┬────────┐
⍝ │Upton │Griselda│Delicia │Prospero│Topaza  │
⍝ ├──────┼────────┼────────┼────────┼────────┤
⍝ │Ellis │Clyde   │Hilary  │Gilles  │Sheridan│
⍝ └──────┴────────┴────────┴────────┴────────┘
⍝ ┌──────┬────────┬────────┬────────┬────────┐
⍝ │Clay  │Carole  │Bellance│Harley  │Nesta   │
⍝ ├──────┼────────┼────────┼────────┼────────┤
⍝ │Sigurd│Dorian  │Renee   │Clinton │Nada    │
⍝ └──────┴────────┴────────┴────────┴────────┘

n[1;1;]
⍝ ┌────────┬─────┬────────┬───────┬──────┐
⍝ │Griselda│Upton│Prospero│Delicia│Topaza│
⍝ └────────┴─────┴────────┴───────┴──────┘
{⍴⍵}n[1;1;]
⍝ 5
{⍳⍴⍵}n[1;1;]
⍝ 1 2 3 4 5
{2 0⍴⍨⍴⍵}n[1;1;]
⍝ 2 0 2 0 2
{(⍳+⍴∘2 0)⍴⍵}n[1;1;]
⍝ 3 2 5 4 7
{⍋(⍳+⍴∘2 0)⍴⍵}n[1;1;]
⍝ 2 1 4 3 5
{⍵[⍋(⍳+⍴∘2 0)⍴⍵]}n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┬──────┐
⍝ │Upton│Griselda│Delicia│Prospero│Topaza│
⍝ └─────┴────────┴───────┴────────┴──────┘
I←{⍵[⍋(⍳+⍴∘2 0)⍴⍵]}⍤1
n
⍝ ┌────────┬──────┬────────┬────────┬────────┐
⍝ │Griselda│Upton │Prospero│Delicia │Topaza  │
⍝ ├────────┼──────┼────────┼────────┼────────┤
⍝ │Clyde   │Ellis │Gilles  │Hilary  │Sheridan│
⍝ └────────┴──────┴────────┴────────┴────────┘
⍝ ┌────────┬──────┬────────┬────────┬────────┐
⍝ │Carole  │Clay  │Harley  │Bellance│Nesta   │
⍝ ├────────┼──────┼────────┼────────┼────────┤
⍝ │Dorian  │Sigurd│Clinton │Renee   │Nada    │
⍝ └────────┴──────┴────────┴────────┴────────┘
I n
⍝ ┌──────┬────────┬────────┬────────┬────────┐
⍝ │Upton │Griselda│Delicia │Prospero│Topaza  │
⍝ ├──────┼────────┼────────┼────────┼────────┤
⍝ │Ellis │Clyde   │Hilary  │Gilles  │Sheridan│
⍝ └──────┴────────┴────────┴────────┴────────┘
⍝ ┌──────┬────────┬────────┬────────┬────────┐
⍝ │Clay  │Carole  │Bellance│Harley  │Nesta   │
⍝ ├──────┼────────┼────────┼────────┼────────┤
⍝ │Sigurd│Dorian  │Renee   │Clinton │Nada    │
⍝ └──────┴────────┴────────┴────────┴────────┘

(⊂⍤⊢⌺(⍪2 2))n[1;1;]
⍝ ┌────────────────┬──────────────────┐
⍝ │┌────────┬─────┐│┌────────┬───────┐│
⍝ ││Griselda│Upton│││Prospero│Delicia││
⍝ │└────────┴─────┘│└────────┴───────┘│
⍝ └────────────────┴──────────────────┘
(⊢⌺(⍪2 2))n[1;1;]
⍝ ┌────────┬───────┐
⍝ │Griselda│Upton  │
⍝ ├────────┼───────┤
⍝ │Prospero│Delicia│
⍝ └────────┴───────┘
(⌽⊢⌺(⍪2 2))n[1;1;]
⍝ ┌───────┬────────┐
⍝ │Upton  │Griselda│
⍝ ├───────┼────────┤
⍝ │Delicia│Prospero│
⍝ └───────┴────────┘
(,∘⌽⊢⌺(⍪2 2))n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┐
⍝ │Upton│Griselda│Delicia│Prospero│
⍝ └─────┴────────┴───────┴────────┘
((,∘⌽⊢⌺(⍪2 2)),⊢/)n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┬──────┐
⍝ │Upton│Griselda│Delicia│Prospero│Topaza│
⍝ └─────┴────────┴───────┴────────┴──────┘
((,∘⌽⊢⌺(⍪2 2)),⊢/)4↑n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┬───────┐
⍝ │Upton│Griselda│Delicia│Prospero│Delicia│
⍝ └─────┴────────┴───────┴────────┴───────┘
(⍴↑(,∘⌽⊢⌺(⍪2 2)),⊢/)4↑n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┐
⍝ │Upton│Griselda│Delicia│Prospero│
⍝ └─────┴────────┴───────┴────────┘
(⍴↑(,∘⌽⊢⌺(⍪2 2)),⊢/)n[1;1;]
⍝ ┌─────┬────────┬───────┬────────┬──────┐
⍝ │Upton│Griselda│Delicia│Prospero│Topaza│
⍝ └─────┴────────┴───────┴────────┴──────┘
S←(⍴↑(,∘⌽⊢⌺(⍪2 2)),⊢/)⍤1
S n
⍝ ┌──────┬────────┬────────┬────────┬────────┐
⍝ │Upton │Griselda│Delicia │Prospero│Topaza  │
⍝ ├──────┼────────┼────────┼────────┼────────┤
⍝ │Ellis │Clyde   │Hilary  │Gilles  │Sheridan│
⍝ └──────┴────────┴────────┴────────┴────────┘
⍝ ┌──────┬────────┬────────┬────────┬────────┐
⍝ │Clay  │Carole  │Bellance│Harley  │Nesta   │
⍝ ├──────┼────────┼────────┼────────┼────────┤
⍝ │Sigurd│Dorian  │Renee   │Clinton │Nada    │
⍝ └──────┴────────┴────────┴────────┴────────┘
'cmpx'⎕CY'dfns'
×/⍴a
⍝ 20000
⍴⍴a
⍝ 5
⍴a
⍝ 2 4 2 2 625
cmpx'E a' 'I a' 'S a'
⍝   E a → 1.5E¯3 |    0% ⎕⎕⎕⎕                                    
⍝   I a → 3.0E¯4 |  -81% ⎕                                       
⍝   S a → 1.6E¯2 | +970% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
S2←(⍴↑(,∘⌽⊢⌺(⍪2 2))⍤1,⊢/)
cmpx'S a' 'S2 a'
⍝   S a  → 1.6E¯2 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   S2 a → 1.5E¯2 | -10% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕    
E
⍝ {⊃,/⌽¨⍵⊂⍨1 0⍴⍨⍴⍵}⍤1
E2←{⊃,/⌽¨⍵⊂⍨1 0⍴⍨⊢/⍴⍵}
cmpx'E a' 'E2 a'
⍝   E a  → 1.5E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   E2 a → 4.7E¯4 | -69% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                           
I
⍝ {⍵[⍋(⍳+⍴∘2 0)⍴⍵]}⍤1
I2←{⍵⌷[⍴⍴⍵]⍨⊂⍋(⍳+⍴∘2 0)⊢/⍴⍵}
cmpx'I a' 'I2 a'
⍝   I a  → 3.0E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   I2 a → 8.0E¯5 | -74% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                             
⍋(⍳+⍴∘2 0)5
⍝ 2 1 4 3 5
⍋(⍳+⍴∘2 0)6
⍝ 2 1 4 3 6 5
(⍳+⍴∘1 ¯1)6
⍝ 2 1 4 3 6 5
(⍳+⍴∘1 ¯1)5
⍝ 2 1 4 3 6
5⌊(⍳+⍴∘1 ¯1)5
⍝ 2 1 4 3 5
5⌊@5(⍳+⍴∘1 ¯1)5
⍝ 2 1 4 3 5
I3←{⍵⌷[⍴⍴⍵]⍨⊂l⌊@l(⍳+⍴∘1 ¯1)l←⊢/⍴⍵}                             
cmpx'I2 a' 'I3 a'                                         
⍝   I2 a → 1.2E¯4 |  0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   I3 a → 1.1E¯4 | -5% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕  
⍴b←,a
⍝ 20000
cmpx'I2 b' 'I3 b'                                         
⍝   I2 b → 1.8E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   I3 b → 1.3E¯4 | -30% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕            
