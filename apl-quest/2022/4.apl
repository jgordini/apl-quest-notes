⍝ Write a function that:
⍝    takes a right argument that is a Boolean scalar or vector.
⍝   returns the length of the longest sequence of consecutive 1s.

s←1 1 1 0 0 1 1 0 1 1 1 1 0
{⍵⊆⍵}s
⍝ ┌─────┬───┬───────┐
⍝ │1 1 1│1 1│1 1 1 1│
⍝ └─────┴───┴───────┘
{≢¨⍵⊆⍵}s
⍝ 3 2 4
{≢¨⊆⍨⍵}s
⍝ 3 2 4
{⌈/≢¨⊆⍨⍵}s
⍝ 4
{⌈/≢¨⊆⍨⍵}0 0 0
⍝ ¯1.797693135E308
{⌈/0,≢¨⊆⍨⍵}0 0 0
⍝ 0
F←{⌈/0,≢¨⊆⍨⍵}
'cmpx'⎕CY'dfns'
b←1=?1e7⍴2
G←{0⌈⌈/≢¨⊆⍨⍵}
cmpx'F b' 'G b'
⍝   F b → 1.9E0  |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   G b → 8.1E¯1 | -57% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                       
s
⍝ 1 1 1 0 0 1 1 0 1 1 1 1 0
{2</⍵}s
⍝ 0 0 0 0 1 0 0 1 0 0 0 0
{2</0,⍵}s
⍝ 1 0 0 0 0 1 0 0 1 0 0 0 0
{2>/0,⍵}s
⍝ 0 0 0 1 0 0 0 1 0 0 0 0 1
{2>/0,⍵}1 1 1 0 0 1 1 0 1 1 1 1
⍝ 0 0 0 1 0 0 0 1 0 0 0 0
{2>/0,⍵,0}1 1 1 0 0 1 1 0 1 1 1 1
⍝ 0 0 0 1 0 0 0 1 0 0 0 0 1
{2</0,⍵,0}s
⍝ 1 0 0 0 0 1 0 0 1 0 0 0 0 0
{⍸2</0,⍵,0}s
⍝ 1 6 9
{⍸2>/0,⍵,0}s
⍝ 4 8 13
{(⍸2</0,⍵,0)-⍸2>/0,⍵,0}s
⍝ ¯3 ¯2 ¯4
{(⍸2>/0,⍵,0)-(⍸2</0,⍵,0)}s
⍝ 3 2 4
{(2>/0,⍵,0)-⍥⍸(2</0,⍵,0)}s
⍝ 3 2 4
{2(>/-⍥⍸</)0,⍵,0}s
⍝ 3 2 4
H←{0⌈⌈/2(>/-⍥⍸</)0,⍵,0}
cmpx'G b' 'H b'
⍝   G b → 2.2E0  |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   H b → 5.9E¯1 | -74% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                             
