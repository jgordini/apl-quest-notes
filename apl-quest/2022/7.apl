⍝ In golf, lower scores place higher – the lowest score places first and the highest score places last.
⍝ Write a function that:
  ⍝  takes a right argument that is a non-decreasing vector or scalar of strictly positive integers, representing a set of scores.
  ⍝  returns a numeric vector of the place for each score; for duplicate scores, it returns the average of the places they hold.

s←67 68 68 69 70 70 70 71 72
⍋s
⍝ 1 2 3 4 5 6 7 8 9
≢s
⍝ 9
⍳≢s
⍝ 1 2 3 4 5 6 7 8 9
s⊆⍳≢s
⍝ ┌─┬───┬─┬─────┬─┬─┐
⍝ │1│2 3│4│5 6 7│8│9│
⍝ └─┴───┴─┴─────┴─┴─┘
(+⌿÷≢)¨s⊆⍳≢s
⍝ 1 2.5 4 6 8 9
(≢⍴+⌿÷≢)¨s⊆⍳≢s
⍝ ┌─┬───────┬─┬─────┬─┬─┐
⍝ │1│2.5 2.5│4│6 6 6│8│9│
⍝ └─┴───────┴─┴─────┴─┴─┘
∊(≢⍴+⌿÷≢)¨s⊆⍳≢s
⍝ 1 2.5 2.5 4 6 6 6 8 9
F←{∊(≢⍴+⌿÷≢)¨⍵⊆⍳≢⍵}
F s
⍝ 1 2.5 2.5 4 6 6 6 8 9

((⊂≢⍴+⌿÷≢)⍤⊢⌸)s
⍝ ┌─┬───────┬─┬─────┬─┬─┐
⍝ │1│2.5 2.5│4│6 6 6│8│9│
⍝ └─┴───────┴─┴─────┴─┴─┘
(∊(⊂≢⍴+⌿÷≢)⍤⊢⌸)s
⍝ 1 2.5 2.5 4 6 6 6 8 9
G←∊(⊂≢⍴+⌿÷≢)⍤⊢⌸
F 5
⍝ 1
G 5
⍝ RANK ERROR
⍝       G 5
⍝       ∧
G←∊(⊂≢⍴+⌿÷≢)⍤⊢⌸⍤,
G 5
⍝ 1
⍳≢s
⍝ 1 2 3 4 5 6 7 8 9
s(⍳≢s)
⍝ ┌──────────────────────────┬─────────────────┐
⍝ │67 68 68 69 70 70 70 71 72│1 2 3 4 5 6 7 8 9│
⍝ └──────────────────────────┴─────────────────┘
↑s(⍳≢s)
⍝ 67 68 68 69 70 70 70 71 72
⍝  1  2  3  4  5  6  7  8  9
⍋s
⍝ 1 2 3 4 5 6 7 8 9
⍒s
⍝ 9 8 5 6 7 4 2 3 1
↑s(⍋s)(⍒s)
⍝ 67 68 68 69 70 70 70 71 72
⍝  1  2  3  4  5  6  7  8  9
⍝  9  8  5  6  7  4  2  3  1
↑s(⍋s)(⌽⍒s)
⍝ 67 68 68 69 70 70 70 71 72
⍝  1  2  3  4  5  6  7  8  9
⍝  1  3  2  4  7  6  5  8  9
(⍳⍤≢+⌽⍤⍒)s
⍝ 2 5 5 8 12 12 12 16 18
(2÷⍨⍳⍤≢+⌽⍤⍒)s
⍝ 1 2.5 2.5 4 6 6 6 8 9
H←2÷⍨⍳⍤≢+⌽⍤⍒
H 5
⍝ RANK ERROR
⍝       H 5
⍝       ∧
H←(2÷⍨⍳⍤≢+⌽⍤⍒),
s⍳s
⍝ 1 2 2 4 5 5 5 8 9
↑s(s⍳s)
⍝ 67 68 68 69 70 70 70 71 72
⍝  1  2  2  4  5  5  5  8  9
s⍸69.5
⍝ 4
s⍸69
⍝ 4
s⍸68
⍝ 3
↑s(s⍳s)(s⍸s)
⍝ 67 68 68 69 70 70 70 71 72
⍝  1  2  2  4  5  5  5  8  9
⍝  1  3  3  4  7  7  7  8  9
2÷⍨(s⍳s)+(s⍸s)
⍝ 1 2.5 2.5 4 6 6 6 8 9
(2÷⍨(⍳⍨)+(⍸⍨))s
⍝ 1 2.5 2.5 4 6 6 6 8 9
(2÷⍨⍳⍨+⍸⍨)5
⍝ RANK ERROR
(2÷⍨⍳⍨+⍸⍨)5
∧
I←(2÷⍨⍳⍨+⍸⍨),
I 5
⍝ 1
I s
⍝ 1 2.5 2.5 4 6 6 6 8 9

'cmpx'⎕cy'dfns'
t←?5E3⍴1E4
10↑t
⍝ 811 8290 9522 1060 8004 2286 2979 6166 1218 5082
t←t[⍋t]
10↑t
⍝ 1 1 2 2 3 5 13 14 14 16
≠10↑t
⍝ 1 0 1 0 1 1 1 1 0 1
+/≠10↑t
⍝ 7
+/≠t
⍝ 3927
cmpx'F t' 'G t' 'H t' 'I t'
⍝   F t → 2.3E¯3 |    0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                          
⍝   G t → 6.7E¯3 | +195% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   H t → 3.5E¯5 |  -99%                                         
⍝   I t → 3.9E¯5 |  -99%                                         
cmpx'H t' 'I t'
⍝   H t → 3.6E¯5 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕           
⍝   I t → 4.9E¯5 | +38% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
]defs
⍝ F ← {∊(≢⍴+⌿÷≢)¨⍵⊆⍳≢⍵}
⍝ G ← ∊⊂≢⍴+⌿÷≢⍤⊢⌸⍤,    
⍝ H ← (2÷⍨⍳⍤≢+⌽⍤⍒),    
⍝ I ← (2÷⍨⍳⍨+⍸⍨),      
