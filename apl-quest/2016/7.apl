⍝ Write a function that takes a numeric vector and returns all elements that are divisible by 3 or 5.
d←0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 0.5 3.5
10 20 30  ∘.×  1 2 3 4
⍝┌→───────────┐
⍝↓10 20 30  40│
⍝│20 40 60  80│
⍝│30 60 90 120│
⍝└~───────────┘
3 5 ∘.| d
⍝┌→────────────────────────────────────────┐
⍝↓0 1 2 0 1 2 0 1 2 0 1 2 0 1 2 0 1 0.5 0.5│
⍝│0 1 2 3 4 0 1 2 3 4 0 1 2 3 4 0 1 0.5 3.5│
⍝└~────────────────────────────────────────┘
0 = 3 5 ∘.| d
⍝┌→────────────────────────────────────┐
⍝↓1 0 0 1 0 0 1 0 0 1 0 0 1 0 0 1 0 0 0│
⍝│1 0 0 0 0 1 0 0 0 0 1 0 0 0 0 1 0 0 0│
⍝└~────────────────────────────────────┘
∨⌿ 0 = 3 5 ∘.| d
⍝┌→────────────────────────────────────┐
⍝│1 0 0 1 0 1 1 0 0 1 1 0 1 0 0 1 0 0 0│
⍝└~────────────────────────────────────┘
(∨⌿ 0 = 3 5 ∘.| d)/d
⍝┌→─────────────────┐
⍝│0 3 5 6 9 10 12 15│
⍝└~─────────────────┘
d /⍨ ∨⌿ 0 = 3 5 ∘.| d
⍝┌→─────────────────┐
⍝│0 3 5 6 9 10 12 15│
⍝└~─────────────────┘
{⍵ /⍨ ∨⌿ 0 = 3 5 ∘.| ⍵} d
⍝┌→─────────────────┐
⍝│0 3 5 6 9 10 12 15│
⍝└~─────────────────┘
{⍵/⍨∨⌿0=3 5∘.|⍵} d
⍝┌→─────────────────┐
⍝│0 3 5 6 9 10 12 15│
⍝└~─────────────────┘
