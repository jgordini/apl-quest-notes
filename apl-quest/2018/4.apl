⍝ Write an APL expression that, given a scalar integer year, returns a character vector (string) of the Chinese zodiac sign for that year. For the purposes of this problem, assume that each year number corresponds to exactly one Chinese zodiac animal.
r
⍝ ┌─────┬────────┬──┬─────┬──────┐
⍝ │2018 │Dog     │  │2012 │Dragon│
⍝ ├─────┼────────┼──┼─────┼──────┤
⍝ │2017 │Rooster │  │2011 │Rabbit│
⍝ ├─────┼────────┼──┼─────┼──────┤
⍝ │2016 │Monkey  │  │2010 │Tiger │
⍝ ├─────┼────────┼──┼─────┼──────┤
⍝ │2015 │Goat    │  │2009 │Ox    │
⍝ ├─────┼────────┼──┼─────┼──────┤
⍝ │2014 │Horse   │  │2008 │Rat   │
⍝ ├─────┼────────┼──┼─────┼──────┤
⍝ │2013 │Snake   │  │2007 │Pig   │
⍝ └─────┴────────┴──┴─────┴──────┘
r[;1 4 2 5]
⍝ ┌─────┬─────┬────────┬──────┐
⍝ │2018 │2012 │Dog     │Dragon│
⍝ ├─────┼─────┼────────┼──────┤
⍝ │2017 │2011 │Rooster │Rabbit│
⍝ ├─────┼─────┼────────┼──────┤
⍝ │2016 │2010 │Monkey  │Tiger │
⍝ ├─────┼─────┼────────┼──────┤
⍝ │2015 │2009 │Goat    │Ox    │
⍝ ├─────┼─────┼────────┼──────┤
⍝ │2014 │2008 │Horse   │Rat   │
⍝ ├─────┼─────┼────────┼──────┤
⍝ │2013 │2007 │Snake   │Pig   │
⍝ └─────┴─────┴────────┴──────┘
r[;1 4 2 5]~¨' '
⍝ ┌────┬────┬───────┬──────┐
⍝ │2018│2012│Dog    │Dragon│
⍝ ├────┼────┼───────┼──────┤
⍝ │2017│2011│Rooster│Rabbit│
⍝ ├────┼────┼───────┼──────┤
⍝ │2016│2010│Monkey │Tiger │
⍝ ├────┼────┼───────┼──────┤
⍝ │2015│2009│Goat   │Ox    │
⍝ ├────┼────┼───────┼──────┤
⍝ │2014│2008│Horse  │Rat   │
⍝ ├────┼────┼───────┼──────┤
⍝ │2013│2007│Snake  │Pig   │
⍝ └────┴────┴───────┴──────┘
⍉r[;1 4 2 5]~¨' '
⍝ ┌──────┬───────┬──────┬────┬─────┬─────┐
⍝ │2018  │2017   │2016  │2015│2014 │2013 │
⍝ ├──────┼───────┼──────┼────┼─────┼─────┤
⍝ │2012  │2011   │2010  │2009│2008 │2007 │
⍝ ├──────┼───────┼──────┼────┼─────┼─────┤
⍝ │Dog   │Rooster│Monkey│Goat│Horse│Snake│
⍝ ├──────┼───────┼──────┼────┼─────┼─────┤
⍝ │Dragon│Rabbit │Tiger │Ox  │Rat  │Pig  │
⍝ └──────┴───────┴──────┴────┴─────┴─────┘
2 12⍴⍉r[;1 4 2 5]~¨' '
⍝ ┌────┬───────┬──────┬────┬─────┬─────┬──────┬──────┬─────┬────┬────┬────┐
⍝ │2018│2017   │2016  │2015│2014 │2013 │2012  │2011  │2010 │2009│2008│2007│
⍝ ├────┼───────┼──────┼────┼─────┼─────┼──────┼──────┼─────┼────┼────┼────┤
⍝ │Dog │Rooster│Monkey│Goat│Horse│Snake│Dragon│Rabbit│Tiger│Ox  │Rat │Pig │
⍝ └────┴───────┴──────┴────┴─────┴─────┴──────┴──────┴─────┴────┴────┴────┘
⌽2 12⍴⍉r[;1 4 2 5]~¨' '
⍝ ┌────┬────┬────┬─────┬──────┬──────┬─────┬─────┬────┬──────┬───────┬────┐
⍝ │2007│2008│2009│2010 │2011  │2012  │2013 │2014 │2015│2016  │2017   │2018│
⍝ ├────┼────┼────┼─────┼──────┼──────┼─────┼─────┼────┼──────┼───────┼────┤
⍝ │Pig │Rat │Ox  │Tiger│Rabbit│Dragon│Snake│Horse│Goat│Monkey│Rooster│Dog │
⍝ └────┴────┴────┴─────┴──────┴──────┴─────┴─────┴────┴──────┴───────┴────┘
↓⌽2 12⍴⍉r[;1 4 2 5]~¨' '
⍝ ┌─────────────────────────────────────────────────────────────┬────────────────────────────────────────────────────────────────────┐
⍝ │┌────┬────┬────┬────┬────┬────┬────┬────┬────┬────┬────┬────┐│┌───┬───┬──┬─────┬──────┬──────┬─────┬─────┬────┬──────┬───────┬───┐│
⍝ ││2007│2008│2009│2010│2011│2012│2013│2014│2015│2016│2017│2018│││Pig│Rat│Ox│Tiger│Rabbit│Dragon│Snake│Horse│Goat│Monkey│Rooster│Dog││
⍝ │└────┴────┴────┴────┴────┴────┴────┴────┴────┴────┴────┴────┘│└───┴───┴──┴─────┴──────┴──────┴─────┴─────┴────┴──────┴───────┴───┘│
⍝ └─────────────────────────────────────────────────────────────┴────────────────────────────────────────────────────────────────────┘
(y s)←↓⌽2 12⍴⍉r[;1 4 2 5]~¨' '
y
⍝ ┌────┬────┬────┬────┬────┬────┬────┬────┬────┬────┬────┬────┐
⍝ │2007│2008│2009│2010│2011│2012│2013│2014│2015│2016│2017│2018│
⍝ └────┴────┴────┴────┴────┴────┴────┴────┴────┴────┴────┴────┘
s
⍝ ┌───┬───┬──┬─────┬──────┬──────┬─────┬─────┬────┬──────┬───────┬───┐
⍝ │Pig│Rat│Ox│Tiger│Rabbit│Dragon│Snake│Horse│Goat│Monkey│Rooster│Dog│
⍝ └───┴───┴──┴─────┴──────┴──────┴─────┴─────┴────┴──────┴───────┴───┘
2006+⍳12
⍝ 2007 2008 2009 2010 2011 2012 2013 2014 2015 2016 2017 2018
12|2006+⍳12
⍝ 3 4 5 6 7 8 9 10 11 0 1 2
0=12|2006+⍳12
⍝ 0 0 0 0 0 0 0 0 0 1 0 0
⍸0=12|2006+⍳12
⍝ 10
9⌽s
⍝ ┌──────┬───────┬───┬───┬───┬──┬─────┬──────┬──────┬─────┬─────┬────┐
⍝ │Monkey│Rooster│Dog│Pig│Rat│Ox│Tiger│Rabbit│Dragon│Snake│Horse│Goat│
⍝ └──────┴───────┴───┴───┴───┴──┴─────┴──────┴──────┴─────┴─────┴────┘
]repr 9⌽s
⍝ 'Monkey' 'Rooster' 'Dog' 'Pig' 'Rat' 'Ox' 'Tiger' 'Rabbit' 'Dragon' 'Snake' 'Horse' 'Goat'
∊9⌽s
⍝ MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat
'MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'∊⎕A
⍝ 1 0 0 0 0 0 1 0 0 0 0 0 0 1 0 0 1 0 0 1 0 0 1 0 1 0 0 0 0 1 0 0 0 0 0 1 0 0 0 0 0 1 0 0 0 0 1 0 0 0 0 1 0 0 0
'MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'(∊⊂⊣)⎕A
⍝ ┌──────┬───────┬───┬───┬───┬──┬─────┬──────┬──────┬─────┬─────┬────┐
⍝ │Monkey│Rooster│Dog│Pig│Rat│Ox│Tiger│Rabbit│Dragon│Snake│Horse│Goat│
⍝ └──────┴───────┴───┴───┴───┴──┴─────┴──────┴──────┴─────┴─────┴────┘
('MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'(∊⊂⊣)⎕A)⌽⍨12|2018
⍝ ┌───┬───┬───┬──┬─────┬──────┬──────┬─────┬─────┬────┬──────┬───────┐
⍝ │Dog│Pig│Rat│Ox│Tiger│Rabbit│Dragon│Snake│Horse│Goat│Monkey│Rooster│
⍝ └───┴───┴───┴──┴─────┴──────┴──────┴─────┴─────┴────┴──────┴───────┘
⊃('MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'(∊⊂⊣)⎕A)⌽⍨12|2018
⍝ Dog
⊃('MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'(∊⊂⊣)⎕A)⌽⍨2018
⍝ Dog

(0∘>)¯5 ¯1 1 5
⍝ 1 1 0 0
(⊢+0∘>)¯5 ¯1 1 5
⍝ ¯4 0 1 5
F←⊃('MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'(∊⊂⊣)⎕A)⌽⍨⊢+0∘> 
F 2018
⍝ Dog
F 1
⍝ Rooster
F ¯1
⍝ Monkey
l←'MonkeyRoosterDogPigRatOxTigerRabbitDragonSnakeHorseGoat'(∊⊂⊣)⎕A
F←⊃l⌽⍨⊢+0∘>
