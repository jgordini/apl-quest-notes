⍝ Given a vector of (X Y) points, or a single X Y point, determine the total distance covered when travelling in a straight line from the first point to the next one, and so on until the last point, then returning directly back to the start. For example, given the points (A B C) ← (¯1.5 ¯1.5)(1.5 2.5)(1.5 ¯1.5), the distance A to B is 5, B to C is 4 and C back to A is 3, for a total of 12. 
p←(¯1.5 ¯1.5)(1.5 2.5)(1.5 ¯1.5)
{⊃⍵}p
⍝ ¯1.5 ¯1.5
{⊂⊃⍵}p
⍝ ┌─────────┐
⍝ │¯1.5 ¯1.5│
⍝ └─────────┘
{⍵,⊂⊃⍵}p
⍝ ┌─────────┬───────┬────────┬─────────┐
⍝ │¯1.5 ¯1.5│1.5 2.5│1.5 ¯1.5│¯1.5 ¯1.5│
⍝ └─────────┴───────┴────────┴─────────┘
{2-/⍵,⊂⊃⍵}p
⍝ ┌─────┬───┬───┐
⍝ │¯3 ¯4│0 4│3 0│
⍝ └─────┴───┴───┘
{{(2*⍨⊃⍵)+(2*⍨⊃⌽⍵)}¨2-/⍵,⊂⊃⍵}p
⍝ 25 16 9
{{((2*⍨⊃⍵)+(2*⍨⊃⌽⍵))*0.5}¨2-/⍵,⊂⊃⍵}p
⍝ 5 4 3
{+/{((2*⍨⊃⍵)+(2*⍨⊃⌽⍵))*0.5}¨2-/⍵,⊂⊃⍵}p
⍝ 12
F←{+/{((2*⍨⊃⍵)+(2*⍨⊃⌽⍵))*0.5}¨2-/⍵,⊂⊃⍵}
{+/{((2*⍨⍺)+(2*⍨⍵))*0.5}/¨2-/⍵,⊂⊃⍵}p
⍝ 12
{{(2*⍨⍵)}¨2-/⍵,⊂⊃⍵}p
⍝ ┌────┬────┬───┐
⍝ │9 16│0 16│9 0│
⍝ └────┴────┴───┘
{{(+/2*⍨⍵)}¨2-/⍵,⊂⊃⍵}p
⍝ 25 16 9
{{(+/2*⍨⍵)*0.5}¨2-/⍵,⊂⊃⍵}p
⍝ 5 4 3
{+/{(+/2*⍨⍵)*0.5}¨2-/⍵,⊂⊃⍵}p
⍝ 12
{+/{0.5*⍨+/2*⍨⍵}¨2-/⍵,⊂⊃⍵}p
⍝ 12
{+/{2*∘÷⍨+/2*⍨⍵}¨2-/⍵,⊂⊃⍵}p
⍝ 12
{+/{2*∘÷⍨+/⍵}¨2*⍨2-/⍵,⊂⊃⍵}p
⍝ 12
{+/2*∘÷⍨{+/⍵}¨2*⍨2-/⍵,⊂⊃⍵}p
⍝ 12
{+/2*∘÷⍨+/¨2*⍨2-/⍵,⊂⊃⍵}p
⍝ 12
{+/2*∘÷⍨+/¨2*⍨⍵-1⌽⍵}p
⍝ 12
G←{+/2*∘÷⍨+/¨2*⍨⍵-1⌽⍵}
p
⍝ ┌─────────┬───────┬────────┐
⍝ │¯1.5 ¯1.5│1.5 2.5│1.5 ¯1.5│
⍝ └─────────┴───────┴────────┘
↑p
⍝ ¯1.5 ¯1.5
⍝  1.5  2.5
⍝  1.5 ¯1.5
1⊖↑p
⍝  1.5  2.5
⍝  1.5 ¯1.5
⍝ ¯1.5 ¯1.5
{2*⍨⍵-1⊖⍵}↑p
⍝ 9 16
⍝ 0 16
⍝ 9  0
{+/2*⍨⍵-1⊖⍵}↑p
⍝ 25 16 9
{+/2*∘÷⍨+/2*⍨⍵-1⊖⍵}↑p
⍝ 12
H←{+/2*∘÷⍨+/2*⍨⍵-1⊖⍵}↑
10 2⍴0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
⍝ 0 0
?10 2⍴0
⍝ 0.01462605079 0.6887547356
⍝ 0.07032848935 0.5450921919
⍝ 0.8893585526  0.3142241915
⍝ 0.345421068   0.3416625649
⍝ 0.3487006392  0.2203553456
⍝ 0.3899769189  0.9379148204
⍝ 0.9226802016  0.2588514351
⍝ 0.9012302952  0.2578073361
⍝ 0.3077261498  0.5348380073
⍝ 0.2126617529  0.0818291547
↓?10 2⍴0
⍝ ┌────────────────────────┬─────────────────────────┬───────────────────────···
⍝ │0.7409423318 0.742374889│0.8843511158 0.7117948415│0.9787273329 0.87973192···
⍝ └────────────────────────┴─────────────────────────┴───────────────────────···
q←↓?1000 2⍴0
'cmpx'⎕CY'dfns'
cmpx'F q' 'G q' 'H q'
⍝   F q → 1.2E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   G q → 2.7E¯4 | -78% ⎕⎕⎕⎕⎕⎕⎕⎕⎕                               
⍝   H q → 2.7E¯5 | -98% ⎕                                       
