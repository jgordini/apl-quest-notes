⍝ Write a dfn that takes a Boolean vector or scalar and “turns off” all the 1s after the first 1.
<\ 0 0 1 1 0
⍝ 0 0 1 0 0
≢0 0 1 1 0
⍝ 5
⍳≢0 0 1 1 0
⍝ 1 2 3 4 5
(⍳∘≢↑¨⊂)0 0 1 1 0
⍝ ┌─┬───┬─────┬───────┬─────────┐
⍝ │0│0 0│0 0 1│0 0 1 1│0 0 1 1 0│
⍝ └─┴───┴─────┴───────┴─────────┘
Prefixes←⍳∘≢↑¨⊂
Prefixes 0 0 1 1 0
⍝ ┌─┬───┬─────┬───────┬─────────┐
⍝ │0│0 0│0 0 1│0 0 1 1│0 0 1 1 0│
⍝ └─┴───┴─────┴───────┴─────────┘
</¨Prefixes 0 0 1 1 0
⍝ 0 0 1 0 0

0 1 ∘.< 0 1
⍝ 0 1
⍝ 0 0
'<' 0 1 ⍪ 0 1 , 0 1 ∘.< 0 1
⍝ < 0 1
⍝ 0 0 1
⍝ 1 0 0

Lt←{⎕←'  ',⍺,'<',⍵,'→',⍺<⍵ ⋄ ⍺<⍵}
LtR←{⎕←' </',⍵,'→',</⍵ ⋄ Lt/⍵}
LtS←{⎕←'<\',⍵,'→',<\⍵ ⋄ LtR¨Prefixes ⍵}
⍝ <\ 0 0 1 1 0
⍝ 0 0 1 0 0
⍝ LtS 0 0 1 1 0
⍝ <\ 0 0 1 1 0 → 0 0 1 0 0
⍝  </ 0 → 0
⍝  </ 0 0 → 0
⍝    0 < 0 → 0
⍝  </ 0 0 1 → 1
⍝    0 < 1 → 1
⍝    0 < 1 → 1
⍝  </ 0 0 1 1 → 0
⍝    1 < 1 → 0
⍝    0 < 0 → 0
⍝    0 < 0 → 0
⍝  </ 0 0 1 1 0 → 0
⍝    1 < 0 → 0
⍝    1 < 0 → 0
⍝    0 < 0 → 0
⍝    0 < 0 → 0
⍝ 0 0 1 0 0
